/* Enclave.edl - Top EDL file. */

enclave {
    from "sgx_tstdc.edl" import *;
    from "sgx_pthread.edl" import *;
    include "sgx_report.h"

    trusted {
        /**
         *  To initliaze the enclave
         */
        public int ecall_init();

        /**
         *  To run a TEE task in enclave
         */
        public int ecall_run(uint32_t type, 
                             [in, string]const char* request_id, 
                             [in, size=data_len]const char* input_data,
                             uint64_t data_len,
                             [user_check] char **output, 
                             [out] uint64_t *output_len);

        /**
         *  To deallocate memory in the enclave
         */
        public void ecall_free();

        /**
         *  To return this enclave id in hex string
         */
        public int ecall_get_enclave_id([user_check] char **output, [out] uint64_t *output_len);

        /**
         *  To generate the enclave quote
         */
        public int ecall_create_report([in, string]const char* request_id, 
                                       [in, string]const char* pubkey_list_hash, 
                                       [in]const sgx_target_info_t* p_qe3_target, 
                                       [out]sgx_report_t* p_report);
    };

    untrusted {
        /*
        *  Output message to console in enclave
        */
        void ocall_printf([in, string] const char *str);
        /*
        *  get current system time (time_t) in GMT from application
        */
        void ocall_get_system_time( [in, out] int64_t** now );
        /*
        *  Output message to log file in enclave
        */
        void ocall_log( uint32_t level, [string, in] char * message );
        /*
        *  malloc a buffer in the untrusted memory
        */
        void ocall_malloc( size_t size, [out] uint8_t **ret );
        /*
        *  free a buffer in the untrusted memory, which is malloc in untrusted code
        */
        void ocall_free( [user_check] uint8_t* p_out_side );
    };

};